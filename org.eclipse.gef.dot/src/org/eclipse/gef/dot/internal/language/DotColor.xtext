/*******************************************************************************
 * Copyright (c) 2016 itemis AG and others.
 *
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     Tamas Miklossy (itemis AG) - initial API and implementation (bug #461506)
 *
 *******************************************************************************/
grammar org.eclipse.gef.dot.internal.language.DotColor hidden(WS)

generate color "http://www.eclipse.org/gef/dot/internal/language/DotColor"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

/*
 * This grammar comes with a custom lexer to disable the COLOR_NUMBER and the COLOR_STRING terminal rules
 * within the context of an RGBColor definition (when the color is defined starting with a '#' symbol)
 * to be able to parse the hex values (such as '#000000' and '#ffffff') properly.
 */

// XXX: While not documented explicitly, an empty color seems to be valid as well (see bug #520919)
Color:
	(RGBColor | HSVColor | StringColor)?
;

RGBColor:
	'#' r=hex g=hex b=hex (a=hex)?
;

HSVColor:
	h=COLOR_NUMBER ','? s=COLOR_NUMBER ','? v=COLOR_NUMBER
;

StringColor:
	("/" (scheme=COLOR_STRING)? "/")?
	name=(HEXADECIMAL_DIGIT | COLOR_STRING | COLOR_NUMBER)
;

hex:
	HEXADECIMAL_DIGIT HEXADECIMAL_DIGIT
;

terminal COLOR_NUMBER:
	('.' DIGITS+) | ZERO_OR_ONE ('.' (DIGITS)+)?
;

terminal HEXADECIMAL_DIGIT:
	DIGITS | 'a' .. 'f' | 'A' .. 'F'
;

terminal COLOR_STRING:
	('a'..'z' | 'A'..'Z' | DIGITS)+
;

terminal fragment ZERO_OR_ONE:
	'0'|'1'
;

terminal fragment DIGITS:
	ZERO_OR_ONE | '2'..'9'
;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;
