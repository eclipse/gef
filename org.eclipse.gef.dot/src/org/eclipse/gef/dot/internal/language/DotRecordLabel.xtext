/*******************************************************************************
 * Copyright (c) 2017 itemis AG and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     Zoey Gerrit Prigge  - initial API and implementation (bug #454629)
 *
 *******************************************************************************/
grammar org.eclipse.gef.dot.internal.language.DotRecordLabel hidden(WS)

generate recordlabel "http://www.eclipse.org/gef/dot/internal/language/DotRecordLabel"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

RLabel:
	fields+=Field ('|' fields+=Field)*;

Field:
	fieldID=FieldID | '{' label=RLabel '}';

FieldID:
	{FieldID} ( portNamed?='<' (port=String)? '>')? (name=String)?;

String:
	NON_WS_STRING+
;

terminal NON_WS_STRING:
	('\\' . | !('|' | '{' | '}' | '<' | '>' | '\\' | ' ' | '\t' | '\r' | '\n'))*;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;